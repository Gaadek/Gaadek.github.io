---
layout: post
title: Mastering PDF table of content
thumbnail-img: /assets/img/sas.png
tags: [sas,pdf,ods,toc,title,level]
---

In this topic I explain how to tune the ODS engine to render a PDF file with a toc (*table of content*) that has a single level for each report. Why do I want that? Just because I like when things are clear, simple and efficient.

# Defaut behavior.
I show here what is the default PDF generated by ODS:
```
options
    orientation=landscape
    papersize=a4
    nodate
    nonumber
;

ods escapechar='~';

ods pdf file=".../cars.pdf" pdftoc=1;

*-- Audi report --*;
title2 j=c height=16pt "~S={foreground=cx00867c}Audi cars";
proc report
    data=sashelp.cars(where = (lowcase(make)='audi'));
run;

*-- BMW report --*;
title2 j=c height=16pt "~S={foreground=cx00867c}BMW cars";
proc report
    data=sashelp.cars(where = (lowcase(make)='bmw'));
run;

ods pdf close;
```

The screenshot below shows the output result. You can notice the 3 levels toc entries:
![Default toc](/assets/img/ods_pdf_toc_01.png)

# Setting text of 1st level TOC entries.
To set the top level entry text with accurate value, we can use the `ods proclabel` statement:
```
options
    orientation=landscape
    papersize=a4
    nodate
    nonumber
;

ods escapechar='~';

ods pdf file=".../cars.pdf" pdftoc=1;

*-- Audi report --*;
title2 j=c height=16pt "~S={foreground=cx00867c}Audi cars";
ods proclabel "Audi";   /* Set the TOC top level text */
proc report
    data=sashelp.cars(where = (lowcase(make)='audi'));
run;

*-- BMW report --*;
title2 j=c height=16pt "~S={foreground=cx00867c}BMW cars";
ods proclabel "BMW";    /* Set the TOC top level text */
proc report
    data=sashelp.cars(where = (lowcase(make)='bmw'));
run;

ods pdf close;
```

The TOC becomes more clear, but we are still getting some extra TOC levels:
![Default toc](/assets/img/ods_pdf_toc_02.png)

# Removing 2nd level TOC entries.
To remove the 2nd level of the toc, we can use the `contents` option of the proc report:
```
options
    orientation=landscape
    papersize=a4
    nodate
    nonumber
;

ods escapechar='~';

ods pdf file=".../cars.pdf" pdftoc=1;

*-- Audi report --*;
title2 j=c height=16pt "~S={foreground=cx00867c}Audi cars";
ods proclabel "Audi";   /* Set the TOC top level text */
proc report
    data=sashelp.cars(where = (lowcase(make)='audi'))
    contents=""         /* Remove 2nd level toc entry */
    ;
run;

*-- BMW report --*;
title2 j=c height=16pt "~S={foreground=cx00867c}BMW cars";
ods proclabel "BMW";    /* Set the TOC top level text */
proc report
    data=sashelp.cars(where = (lowcase(make)='bmw'))
    contents=""         /* Remove 2nd level toc entry */
    ;
run;

ods pdf close;
```

Here, we're close to the final goal:
![Default toc](/assets/img/ods_pdf_toc_03.png)

# Removing 3rd level TOC entries.
This part is the more complex because there is no option to do that with ease. We must implement breaking within the `proc report` to set the page contents:
```break before xxx / page contents='';```

However, we have to either identify or build the `xxx` variable to break on. Usualy, I add this variable in the original dataset to avoid data loss:
```
options
    orientation=landscape
    papersize=a4
    nodate
    nonumber
;

ods escapechar='~';

ods pdf file=".../cars.pdf" pdftoc=1;

*-- Creating dataset with a variable to reset 3rd level toc entries --*;
data cars2;
	set sashelp.cars;
	pgb = 'x';
run;

*-- Audi report --*;
title2 j=c height=16pt "~S={foreground=cx00867c}Audi cars";
ods proclabel "Audi";   /* Set the TOC top level text */
proc report
    data=cars2(where = (lowcase(make)='audi'))
    contents=""         /* Remove 2nd level toc entry */
    ;
    
    /* Remove 3rd level toc entry */
    define pgb / noprint order;
    break before pgb / page contents='';
run;

*-- BMW report --*;
title2 j=c height=16pt "~S={foreground=cx00867c}BMW cars";
ods proclabel "BMW";    /* Set the TOC top level text */
proc report
    data=cars2(where = (lowcase(make)='bmw'))
    contents=""         /* Remove 2nd level toc entry */
    ;
    
    /* Remove 3rd level toc entry */
    define pgb / noprint order;
    break before pgb / page contents='';
run;

ods pdf close;
```

Here, we get the exepected result: a toc with 1 entry per report:
![Default toc](/assets/img/ods_pdf_toc_04.png)
